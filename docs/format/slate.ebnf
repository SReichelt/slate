# This is an informal EBNF grammar (in W3C format) for .slate files.
# Details about whitespace, identifiers, and strings are omitted.
# .slate files are always encoded in UTF-8.

Document                ::= MetaModelRef Definition*;

MetaModelRef            ::= '%' ParentPath Identifier '%';

Definition              ::= '$' Identifier ParameterList? ':' Expression '{' Definition* NamedArguments '}';

ParameterList           ::= '(' Parameters ')';
Parameters              ::= (ParameterGroup (',' ParameterGroup)*)?;
ParameterGroup          ::= ParameterSpecifier (',' ParameterSpecifier)* ':' Expression ('=' Expression)?;
ParameterSpecifier      ::= Identifier ArrayExpression? '...'? '?'?;

ArgumentList            ::= '(' Arguments ')';
Arguments               ::= (Argument (',' Argument)*)?;
Argument                ::= NamedArgument | Expression;
NamedArguments          ::= (NamedArgument (',' NamedArgument)*)?;
NamedArgument           ::= Identifier '=' Expression;

Expression              ::= IntegerExpression | StringExpression | VariableRefExpression | MetaRefExpression | DefinitionRefExpression | ParameterExpression | CompoundExpression | ArrayExpression | IndexedExpression | '?';
IntegerExpression       ::= ('+' | '-')? [0-9]+;
StringExpression        ::= <C-style string enclosed in single quotes>;
VariableRefExpression   ::= Identifier;
MetaRefExpression       ::= '%' IdentifierWithArguments;
DefinitionRefExpression ::= '$' Path;
ParameterExpression     ::= '#' ParameterList;
CompoundExpression      ::= '{' Arguments '}';
ArrayExpression         ::= '[' (Expression (',' Expression)*)? ']';
IndexedExpression       ::= Expression '[' Arguments ']';

Path                    ::= ParentPath IdentifierWithArguments ('.' IdentifierWithArguments)*;
ParentPath              ::= ((Identifier | '.' | '..') '/')*
IdentifierWithArguments ::= Identifier ArgumentList?
Identifier              ::= <nonempty string of characters except whitespace and punctuation (see isSpecialCharacter)> | <C-style string enclosed in double quotes>;
