%../../../../logics/hlm%

/**
 * @remarks Strictly speaking, the elements of this set are not magmas but isomorphism classes of magmas. Due to the rules of HLM, it is not even possible to define magmas (or other structures) in a way that would distinguish two isomorphic structures. In particular, the requirement to specify an equality definition, together with the way two arbitrary sets are treated as separate types, make sure that in the most general case, equality is in fact the same as isomorphism. (It is possible to identify more structures, but not fewer.)
 *
 * @references * https://en.wikipedia.org/wiki/Magma_(algebra)
 */
$Magmas: %Construction {
  $magma(
    S: %Set,
    ∗: %Element($../../Essentials/Functions/Operations/"Inner operations"(X = {S}))
  ): %Constructor {
    display = [$Tuple(
      items = [S, ∗],
      style = '[]'
    )],
    equalityDefinition = {
      leftParameters = #(
        S: %Set,
        ∗: %Element($../../Essentials/Functions/Operations/"Inner operations"(X = {S}))
      ),
      rightParameters = #(
        T: %Set,
        ⋆: %Element($../../Essentials/Functions/Operations/"Inner operations"(X = {T}))
      ),
      definition = [%exists(
        #(φ: %Element($../../Essentials/Functions/Bijections(
          X = {S},
          Y = {T}
        ))),
        formula = %equals(
          $../Isomorphisms/"inner operation transform"(
            X = {S},
            Y = {T},
            φ = {φ},
            ∗ = {∗}
          ),
          ⋆
        )
      )],
      isomorphic = %true
    }
  }

  display = ['𝓜𝓪𝓰'],
  definitionDisplay = {
    parameter = #(𝐌: %Expr),
    singularName = 'magma',
    pluralName = 'magmas'
  }
}
