%../../../../logics/hlm%

/**
 * @remarks This definition uses a feature of the HLM logic called “bindings.” It is [equivalent](mapping%20as%function) to the usual set-theoretic definition, which is called “[mapping](../Relations/mapping)” in this library to distinguish the two definitions.
 *
 *          The major advantage of the definition using bindings is that given an explicitly defined function, determining the value at a specific input is simply a matter of resolving definitions.
 *
 *          Function [extensionality](https://en.wikipedia.org/wiki/Extensionality) is a consequence of the requirement to specify an equality definition for the constructor.
 *
 * @references * https://en.wikipedia.org/wiki/Function_(mathematics)
 *             * http://mathworld.wolfram.com/Function.html
 *             * https://proofwiki.org/wiki/Definition:Mapping
 */
$Functions(
  X: %Set,
  Y: %Set
): %Construction {
  $function(x: %Binding(
    X,
    #(y: %Element(Y))
  )): %Constructor {
    display = [$Parens(
      body = $Aligned(items = [
        [
          $InnerParens(
            body = X,
            maxLevel = 0
          ),
          ' → ',
          $InnerParens(
            body = Y,
            maxLevel = 0
          )
        ],
        [x, ' ↦ ', y]
      ]),
      style = '()'
    )],
    definitionDisplay = {
      parameter = #(f: %Expr),
      display = [$FunctionPrefix(
        function = f,
        body = [
          ' ',
          $Aligned(items = [
            [
              $InnerParens(
                body = X,
                maxLevel = 0
              ),
              ' → ',
              $InnerParens(
                body = Y,
                maxLevel = 0
              )
            ],
            [x, ' ↦ ', y]
          ])
        ]
      )]
    },
    equalityDefinition = {
      leftParameters = #(x: %Binding(
        X,
        #(y: %Element(Y))
      )),
      rightParameters = #(x: %Binding(
        X,
        #("y'": %Element(Y))
      )),
      definition = [%forall(
        #(x: %Element(X)),
        %equals(y[x], "y'"[x])
      )],
      isomorphic = %true
    }
  }

  display = [$FunctionOperator(
    symbol = '→',
    operands = [X, Y]
  )],
  definitionDisplay = {
    parameter = #(f: %Expr),
    display = [$FunctionDefinition(
      function = f,
      symbol = '→',
      operands = [X, Y]
    )],
    singularName = 'function',
    pluralName = 'functions',
    nameOptional = %true
  }
}
