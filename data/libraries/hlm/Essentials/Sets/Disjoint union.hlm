%../../../../logics/hlm%

/**
 * @remarks In contrast to the definition of set union, the disjoint union of sets is a “construction,” which is an HLM-specific concept very similar to an inductive definition in type theory. The disjoint union has two “constructors.”
 *
 *          A construction always includes definitions of equality for each constructor. In this case, those definitions are trivial. Different constructors are never considered equal.
 *
 * @references * https://en.wikipedia.org/wiki/Disjoint_union
 *             * http://mathworld.wolfram.com/DisjointUnion.html
 *             * https://proofwiki.org/wiki/Definition:Disjoint_Union_(Set_Theory)
 */
$"Disjoint union"(
  S: %Set(
    auto = %false,
    embedSubsets = %true
  ),
  T: %Set(
    auto = %false,
    embedSubsets = %true
  )
): %Construction {
  $left(s: %Element(S)): %Constructor {
    display = [$Constructor(
      name = 'l',
      arguments = [s]
    )],
    equalityDefinition = {
      leftParameters = #(s: %Element(S)),
      rightParameters = #("s'": %Element(S)),
      definition = [%equals(s, "s'")],
      isomorphic = %true
    }
  }

  $right(t: %Element(T)): %Constructor {
    display = [$Constructor(
      name = 'r',
      arguments = [t]
    )],
    equalityDefinition = {
      leftParameters = #(t: %Element(T)),
      rightParameters = #("t'": %Element(T)),
      definition = [%equals(t, "t'")],
      isomorphic = %true
    }
  }

  display = [$Operator(
    symbol = '⊎',
    operands = [S, T],
    space = ' '
  )]
}
