%../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Closure_(mathematics)#Binary_relation_closures: reflexive transitive closure
 *             * https://proofwiki.org/wiki/Definition:Reflexive_Transitive_Closure
 */
$"reflexive transitive closure"(
  S: %Set(auto = %true),
  ≺: %Element($Endorelations(S = {S}))
): %ExplicitOperator {
  display = [$SubSup(
    body = ≺,
    sup = '∗'
  )],
  definition = [
    $closure(
      S = {S},
      ≺ = {≺},
      p = {$../Properties/predicate(
        S = {$Endorelations(S = {S})},
        parameter = #(_1: %Element($Endorelations(S = {S}))),
        predicate = $preorder(
          S = {S},
          ≺ = {_1}
        )
      )}
    ),
    $"reflexive closure"(
      S = {S},
      ≺ = {$"transitive closure"(
        S = {S},
        ≺ = {≺}
      )}
    ),
    $"transitive closure"(
      S = {S},
      ≺ = {$"reflexive closure"(
        S = {S},
        ≺ = {≺}
      )}
    )
  ]
}
