%../../../../../logics/hlm%

/**
 * @remarks Note that in addition to this definition for natural numbers, there is a compatible [definition for integers](../Integer/coprime).
 *
 * @references * https://en.wikipedia.org/wiki/Coprime_integers
 *             * http://mathworld.wolfram.com/RelativelyPrime.html
 *             * https://proofwiki.org/wiki/Definition:Coprime/Integers
 *             * https://coq.inria.fr/library/Coq.ZArith.Znumtheory.html#rel_prime (for integers)
 */
$coprime(
  m: %Element($"Natural numbers"),
  n: %Element(%previous)
): %Predicate {
  display = [$MultiProperty(
    operands = [m, n],
    property = 'coprime'
  )],
  definition = [
    %forall(
      #(
        a: %Element($Subsets/"Positive numbers"),
        _1: %Constraint($divisible(
          m = {a},
          n = {m}
        )),
        _2: %Constraint($divisible(
          m = {a},
          n = {n}
        ))
      ),
      %equals(
        a,
        $number(value = 1)
      )
    ),
    %not(%exists(
      #(b: %Element($Subsets/"Segment (greater)"(n = {$number(value = 1)}))),
      formula = %and(
        $divisible(
          m = {b},
          n = {m}
        ),
        $divisible(
          m = {b},
          n = {n}
        )
      )
    )),
    %sub(
      $../../Sets/Intersection(
        U = {$"Natural numbers"},
        S = {$Divisors(n = {m})},
        T = {$Divisors(n = {n})}
      ),
      %enumeration($number(value = 1))
    ),
    %setEquals(
      $../../Sets/Intersection(
        U = {$"Natural numbers"},
        S = {$Divisors(n = {m})},
        T = {$Divisors(n = {n})}
      ),
      %enumeration($number(value = 1))
    ),
    %not(%exists(
      #(p: %Element($Prime/"Prime numbers")),
      formula = %and(
        $divisible(
          m = {p},
          n = {m}
        ),
        $divisible(
          m = {p},
          n = {n}
        )
      )
    ))
  ]
}
