%../../../../../logics/hlm%

/**
 * @remarks The first definition uses induction on both _n_ and _k_. It is more explicit because it does not rely on division of natural numbers.
 *
 * @references * https://en.wikipedia.org/wiki/Binomial_coefficient
 *             * http://mathworld.wolfram.com/BinomialCoefficient.html
 *             * https://proofwiki.org/wiki/Definition:Binomial_Coefficient
 *             * https://coq.inria.fr/library/Coq.Reals.Binomial.html#C
 */
$"binomial coefficient"(
  n: %Element($"Natural numbers"),
  k: %Element(%previous)
): %ExplicitOperator {
  display = [$BinomialCoefficient(
    top = n,
    bottom = k
  )],
  definition = [
    %structuralCases(
      k,
      $"Natural numbers",
      [
        {
          constructor = $"Natural numbers".zero,
          value = $number(value = 1),
          rewrite = %true
        },
        {
          constructor = $"Natural numbers".successor,
          parameters = #(x: %Element($"Natural numbers")),
          value = %structuralCases(
            n,
            $"Natural numbers",
            [
              {
                constructor = $"Natural numbers".zero,
                value = $number(value = 0),
                rewrite = %true
              },
              {
                constructor = $"Natural numbers".successor,
                parameters = #(m: %Element($"Natural numbers")),
                value = $sum(
                  m = {$"binomial coefficient"(
                    n = {m},
                    k = {x}
                  )},
                  n = {$"binomial coefficient"(
                    n = {m},
                    k = {k}
                  )}
                ),
                rewrite = %true
              }
            ]
          ),
          rewrite = %true
        }
      ]
    ),
    %cases([
      {
        formula = $"less or equal"(
          n = {n},
          m = {k}
        ),
        value = $quotient(
          n = {$factorial(n = {n})},
          m = {$product(
            m = {$factorial(n = {k})},
            n = {$factorial(n = {$difference(
              n = {n},
              m = {k}
            )})}
          )}
        )
      },
      {
        formula = $less(
          m = {n},
          n = {k}
        ),
        value = $number(value = 0)
      }
    ])
  ]
}
