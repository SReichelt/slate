%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Proofs_involving_the_addition_of_natural_numbers#Proof_of_identity_element
 *             * https://proofwiki.org/wiki/Identity_Element_of_Natural_Number_Addition_is_Zero
 *             * https://proofwiki.org/wiki/Natural_Number_Addition_Commutes_with_Zero
 */
$"Left-addition of zero is neutral"(n: %Element($"Natural numbers")): %StandardTheorem {
  claim = %equals(
    $sum(
      m = {$number(value = 0)},
      n = {n}
    ),
    n
  ),
  proofs = [{
    goal = %equals(
      %structuralCases(
        n,
        $"Natural numbers",
        [
          {
            constructor = $"Natural numbers".zero,
            value = $number(value = 0)
          },
          {
            constructor = $"Natural numbers".successor,
            parameters = #(x: %Element($"Natural numbers")),
            value = $"Natural numbers".successor(n = {$sum(
                m = {$number(value = 0)},
                n = {x}
            )})
          }
        ]
      ),
      n
    ),
    steps = #(_: %ProveCases(
      side = %left,
      caseProofs = [
        {
          goal = %equals(
            $number(value = 0),
            $number(value = 0)
          ),
          steps = #()
        },
        {
          parameters = #(x: %Element($"Natural numbers")),
          goal = %equals(
            $"Natural numbers".successor(n = {$sum(
                m = {$number(value = 0)},
                n = {x}
            )}),
            $"Natural numbers".successor(n = {x})
          ),
          steps = #(
            _: %UseTheorem(
              $"Left-addition of zero is neutral"(n = {x}),
              %equals(
                $sum(
                  m = {$number(value = 0)},
                  n = {x}
                ),
                x
              )
            ),
            _: %Extend($"Natural numbers".successor(n = {%previous}))
          )
        }
      ]
    ))
  }]
}
