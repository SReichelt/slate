%../../../../../logics/hlm%

/**
 * @remarks Such recursive definitions are possible due to the definition of natural numbers as a construction (i.e. inductive data type).
 *
 * @references * https://en.wikipedia.org/wiki/Natural_number#Addition
 *             * https://proofwiki.org/wiki/Definition:Addition/Natural_Numbers
 *             * https://coq.inria.fr/library/Coq.Init.Nat.html#add
 *             * https://coq.inria.fr/library/Coq.NArith.BinNatDef.html#N.add
 */
$sum(
  m: %Element($"Natural numbers"),
  n: %Element(%previous)
): %ExplicitOperator {
  display = [$Operator(
    symbol = '+',
    operands = [m, n],
    minParenLevel = 1,
    maxParenLevel = 1
  )],
  definition = [%structuralCases(
    n,
    $"Natural numbers",
    [
      {
        constructor = $"Natural numbers".zero,
        value = m
      },
      {
        constructor = $"Natural numbers".successor,
        parameters = #(x: %Element($"Natural numbers")),
        value = $"Natural numbers".successor(n = {$sum(
            m = {m},
            n = {x}
        )})
      }
    ]
  )]
}
