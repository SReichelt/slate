%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Coprime_integers
 *             * http://mathworld.wolfram.com/RelativelyPrime.html
 *             * https://proofwiki.org/wiki/Definition:Coprime/Integers
 *             * https://coq.inria.fr/library/Coq.ZArith.Znumtheory.html#rel_prime
 */
$coprime(
  a: %Element($Integers),
  b: %Element(%previous)
): %Predicate {
  display = [$MultiProperty(
    operands = [a, b],
    property = 'coprime'
  )],
  definition = [
    %forall(
      #(
        c: %Element($Subsets/"Positive numbers"),
        _1: %Constraint($divisible(
          a = {c},
          b = {a}
        )),
        _2: %Constraint($divisible(
          a = {c},
          b = {b}
        ))
      ),
      %equals(
        c,
        $../Natural/number(value = 1)
      )
    ),
    %not(%exists(
      #(d: %Element($Subsets/"Segment (greater)"(a = {$../Natural/number(value = 1)}))),
      formula = %and(
        $divisible(
          a = {d},
          b = {a}
        ),
        $divisible(
          a = {d},
          b = {b}
        )
      )
    )),
    %forall(
      #(
        e: %Element($Subsets/"Nonzero numbers"),
        _3: %Constraint($divisible(
          a = {e},
          b = {a}
        )),
        _4: %Constraint($divisible(
          a = {e},
          b = {b}
        ))
      ),
      %equals(
        $absolute(a = {e}),
        $../Natural/number(value = 1)
      )
    ),
    %sub(
      $../../Sets/Intersection(
        U = {$Integers},
        S = {$Divisors(b = {a})},
        T = {$Divisors(b = {b})}
      ),
      %enumeration(
        $../Natural/number(value = 1),
        $inverse(a = {$../Natural/number(value = 1)})
      )
    ),
    %setEquals(
      $../../Sets/Intersection(
        U = {$Integers},
        S = {$Divisors(b = {a})},
        T = {$Divisors(b = {b})}
      ),
      %enumeration(
        $../Natural/number(value = 1),
        $inverse(a = {$../Natural/number(value = 1)})
      )
    ),
    %not(%exists(
      #(p: %Element($../Natural/Prime/"Prime numbers")),
      formula = %and(
        $divisible(
          a = {p},
          b = {a}
        ),
        $divisible(
          a = {p},
          b = {b}
        )
      )
    ))
  ]
}
