%../../../../../logics/hlm%

/**
 * @remarks Note that the sums on the right are sums of natural numbers.
 *
 * @references * https://en.wikipedia.org/wiki/Integer#Construction
 *             * https://proofwiki.org/wiki/Definition:Addition/Integers
 *             * https://coq.inria.fr/library/Coq.ZArith.BinIntDef.html#Z.add (but uses a different encoding)
 */
$sum(
  a: %Element($Integers),
  b: %Element(%previous)
): %ExplicitOperator {
  display = [$Operator(
    symbol = '+',
    operands = [a, b],
    minParenLevel = 1,
    maxParenLevel = 1
  )],
  definition = [%structuralCases(
    b,
    $Integers,
    [{
      constructor = $Integers.difference,
      parameters = #(
        n_b: %Element($../Natural/"Natural numbers"),
        m_b: %Element(%previous)
      ),
      value = %structuralCases(
        a,
        $Integers,
        [{
          constructor = $Integers.difference,
          parameters = #(
            n_a: %Element($../Natural/"Natural numbers"),
            m_a: %Element(%previous)
          ),
          value = $Integers.difference(
              n = {$../Natural/sum(
                m = {n_a},
                n = {n_b}
              )},
              m = {$../Natural/sum(
                m = {m_a},
                n = {m_b}
              )}
          )
        }]
      )
    }]
  )]
}
