%../../../../../logics/hlm%

/**
 * @remarks Note that the sums and products on the right are operations on natural numbers.
 *
 * @references * https://coq.inria.fr/library/Coq.ZArith.BinIntDef.html#Z.mul (but uses a different encoding)
 */
$product(
  a: %Element(
    $Integers,
    shortcut = {
      constructor = $Integers.difference,
      parameters = #(
        n_a: %Element($../Natural/"Natural numbers"),
        m_a: %Element(%previous)
      ),
      rewrite = %true
    }
  ),
  b: %Element(
    %previous,
    shortcut = {
      constructor = $Integers.difference,
      parameters = #(
        n_b: %Element($../Natural/"Natural numbers"),
        m_b: %Element(%previous)
      ),
      rewrite = %true
    }
  )
): %ExplicitOperator {
  display = [$Operator(
    symbol = '⋅',
    operands = [a, b],
    space = ' ',
    minParenLevel = 2,
    maxParenLevel = 2
  )],
  definition = [$Integers.difference(
      n = {$../Natural/sum(
        m = {$../Natural/product(
          m = {n_a},
          n = {n_b}
        )},
        n = {$../Natural/product(
          m = {m_a},
          n = {m_b}
        )}
      )},
      m = {$../Natural/sum(
        m = {$../Natural/product(
          m = {n_a},
          n = {m_b}
        )},
        n = {$../Natural/product(
          m = {m_a},
          n = {n_b}
        )}
      )}
  )]
}
