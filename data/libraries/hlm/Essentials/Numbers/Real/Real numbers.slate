%../../../../../logics/hlm%

$"Real numbers": %Construction {
  $class(
    r: %Element($../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"})),
    _1: %Constraint($../Rational/Sequences/cauchy(𝐚 = {r}))
  ): %Constructor {
    display = [$Parens(
      body = r,
      style = '[]'
    )],
    equalityDefinition = {
      leftParameters = #(
        r: %Element(
          $../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"}),
          shortcut = {
            constructor = $../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"}).sequence,
            parameters = #(n: %Binding(
              $../Natural/"Natural numbers",
              #(a: %Element($../Rational/"Rational numbers"))
            ))
          }
        ),
        _1: %Constraint($../Rational/Sequences/cauchy(𝐚 = {r}))
      ),
      rightParameters = #(
        "r'": %Element(
          $../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"}),
          shortcut = {
            constructor = $../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"}).sequence,
            parameters = #(n: %Binding(
              $../Natural/"Natural numbers",
              #("a'": %Element($../Rational/"Rational numbers"))
            ))
          }
        ),
        _2: %Constraint($../Rational/Sequences/cauchy(𝐚 = {"r'"}))
      ),
      definition = [
        $../Rational/Sequences/converges(
          𝐚 = {$../Rational/Sequences/"sequence difference"(
            𝐛 = {r},
            𝐚 = {"r'"}
          )},
          l = {$../Natural/number(value = 0)}
        ),
        %forall(
          #(ε: %Element($../Rational/Subsets/"Positive numbers")),
          %exists(
            #(n: %Element($../Natural/"Natural numbers")),
            formula = %forall(
              #(m: %Element($../Natural/Subsets/"Segment (greater or equal)"(n = {n}))),
              $../Rational/less(
                a = {$../Rational/absolute(a = {$../Rational/difference(
                  b = {a[m]},
                  a = {"a'"[m]}
                )})},
                b = {ε}
              )
            )
          )
        )
      ]
    }
  }

  display = ['ℝ'],
  embedding = {
    parameter = #(x: %Element($../Rational/"Rational numbers")),
    target = $"Real numbers".class(r = {$../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"}).sequence(n = {
          #(k: %Element($../Natural/"Natural numbers")),
          {a = {x}}
    })})
  }
}
