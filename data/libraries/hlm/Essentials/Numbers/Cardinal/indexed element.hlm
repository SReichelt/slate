%../../../../../logics/hlm%

/**
 * @remarks This definition is used to prove the equivalence of alternative definitions of [finiteness](../../Sets/finite). It should probably be refactored into an operator that produces a finite sequence from a finite set of natural numbers.
 */
$"indexed element"(
  M: %Subset($../Natural/"Natural numbers"),
  n: %Element($../Natural/"Natural numbers"),
  _1: %Constraint($less(
    𝔵 = {n},
    𝔶 = {$"Cardinal numbers".cardinality(S = {M})}
  )),
  m: %Def($../Natural/minimum(M = {M}))
): %ExplicitOperator {
  display = [$Function(
    function = M,
    arguments = [n],
    style = '[]'
  )],
  definition = [%structuralCases(
    n,
    $../Natural/"Natural numbers",
    [
      {
        constructor = $../Natural/"Natural numbers".zero,
        value = m,
        rewrite = %true
      },
      {
        constructor = $../Natural/"Natural numbers".successor,
        parameters = #(x: %Element($../Natural/"Natural numbers")),
        value = $"indexed element"(
          M = {$../../Sets/"Relative complement"(
            S = {M},
            T = {%enumeration(m)}
          )},
          n = {x}
        ),
        rewrite = %true
      }
    ]
  )]
}
